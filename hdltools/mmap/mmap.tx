AXIDescription:
  (static_declarations*=StaticStatement params*=ParameterStatement statements*=Statement)#;
StaticStatement:
  '#' var=ID value=StaticStmtValue ';';
ParameterStatement:
  'param' name=ID value=StaticStmtValue ';';
StaticStmtValue:
    StaticValue | FnCall;
FnCall:
    '$' fn=ID '(' args*=FnArgument[','] ')';
FnArgument:
    ID | INT;
Statement:
  (SlaveRegister | SlaveRegisterField | SlavePort | GenerateStatement) ';';
SlaveRegister:
  /register\s+/ name=TemplatedNameSubst ('at' address=PositiveIntegerValue )?
  properties*=RegisterProperty;
TemplatedNameSubst[noskipws]:
  fragments += TemplatedNameSubstFragment;
TemplatedNameSubstFragment:
  fragment=/[a-zA-Z0-9_]*/ templates*=TemplatedNameSubstFmt;
TemplatedNameSubstFmt:
  '{' arg=/[0-9a-zA-Z_\-\+\*\/]+/ '}';

SlaveRegisterField:
    SlaveRegisterFieldImplicit | SlaveRegisterFieldExplicit;

SlaveRegisterFieldExplicit:
    /field\s+/ source=SourceBitAccessor
    (position=RegisterFieldPosition access=RegisterFieldPermission default=RegisterFieldDefault?
    properties*=RegisterProperty qualifiers*=RegisterFieldQualifier)#;

SlaveRegisterFieldImplicit:
    /field\s+/ source=SourceBitAccessor position=BitField access=AccessPermission default=StaticValue?;

RegisterFieldPosition:
    'position' '=' position=BitField;
RegisterFieldPermission:
    'access' '=' access=AccessPermission;
RegisterFieldDefault:
    'default' '=' default=DefaultValue;
RegisterFieldQualifier:
    'autoclear';
DefaultValue:
    StaticValue | TemplatedNameSubst;

SlavePort:
  SlaveOutput | SlaveInput;
SlaveOutput:
  trigger?=/trigger\s+/ /output\s+/ desc=OutputDescriptor;
SlaveInput:
  /input\s+/ desc=InputDescriptor;
OutputDescriptor:
  name=TemplatedNameSubst sig=SignalSource;
InputDescriptor:
  name=TemplatedNameSubst sig=SignalDestination;
SignalSource:
  'source' '=' dest=SourceDestField;
SignalDestination:
  'dest' '=' dest=SourceDestField;
SourceBitAccessor:
    register=TemplatedNameSubst '.' bit=ID;
SourceDestField:
  SourceBitAccessor | TemplatedNameSubst | ID;
FieldBitAccessor:
    register=ID '.' bit=ID;
GenerateStatement:
    'for' var=ID 'in' range=Range 'generate' gen_scope+=Statement 'end';
Range:
    '<' left=StaticValue ',' right=StaticValue '>';
RegisterProperty:
  name=ID '=' '"' value=/[^"]+/ '"';
StaticValue:
  ID | PositiveIntegerValue;
PositiveIntegerValue:
  hex=Hex | posint=PositiveInteger;
BitField:
  left=BitFieldValue ('..' right=BitFieldValue)?;
BitFieldValue:
    PositiveInteger | TemplatedNameSubst;
Hex:
  '0x' /[0-9a-fA-F]+/;
PositiveInteger:
  /[0-9]+/;
AccessPermission:
  'RW' | 'R' | 'W' ;
Comment:
  /\/\/.*$/;
